/// https://github.com/WebAssembly/WASI/blob/v0.2.7/legacy/preview1/docs.md#errno
pub const Errno = enum(u16) {
    success,
    @"2big",
    acces,
    addrinuse,
    addrnotavail,
    afnosupport,
    again,
    already,
    badf,
    badmsg,
    busy,
    canceled,
    child,
    connaborted,
    connrefused,
    connreset,
    deadlk,
    destaddrreq,
    dom,
    dquot,
    exist,
    fault,
    fbig,
    hostunreach,
    idrm,
    ilseq,
    inprogress,
    intr,
    inval,
    io,
    isconn,
    isdir,
    loop,
    mfile,
    mlink,
    msgsize,
    multihop,
    nametoolong,
    netdown,
    netreset,
    netunreach,
    nfile,
    nobufs,
    nodev,
    noent,
    noexec,
    nolck,
    nolink,
    nomem,
    nomsg,
    noprotoopt,
    nospc,
    nosys,
    notconn,
    notdir,
    notempty,
    notrecoverable,
    notsock,
    notsup,
    notty,
    nxio,
    overflow,
    ownerdead,
    perm,
    pipe,
    proto,
    protonosupport,
    prototype,
    range,
    rofs,
    spipe,
    srch,
    stale,
    timedout,
    txtbsy,
    xdev,
    notcapable,
    _,

    pub fn message(num: Errno) ?[:0]const u8 {
        return switch (num) {
            .success => "No error occurred. System call completed successfully.",
            .@"2big" => "Argument list too long.",
            .acces => "Permission denied",
            .addrinuse => "Address in use.",
            .addrnotavail => "Address not available.",
            .afnosupport => "Address family not supported.",
            .again => "Resource unavailable, or operation would block.",
            .already => "Connection already in progress.",
            .badf => "Bad file descriptor.",
            .badmsg => "Bad message.",
            .busy => "Device or resource busy.",
            .canceled => "Operation canceled.",
            .child => "No child processes.",
            .connaborted => "Connection aborted.",
            .connrefused => "Connection refused.",
            .connreset => "Connection reset.",
            .deadlk => "Resource deadlock would occur.",
            .destaddrreq => "Destination address required.",
            .dom => "Mathematics argument out of domain of function.",
            .dquot => "Reserved (Disk quota exceeded).",
            .exist => "File exists.",
            .fault => "Bad address.",
            .fbig => "File too large.",
            .hostunreach => "Host is unreachable.",
            .idrm => "Identifier removed.",
            .ilseq => "Illegal byte sequence.",
            .inprogress => "Operation in progress.",
            .intr => "Interrupted function.",
            .inval => "Invalid argument.",
            .io => "I/O error.",
            .isconn => "Socket is connected.",
            .isdir => "Is a directory.",
            .loop => "Too many levels of symbolic links.",
            .mfile => "File descriptor value too large.",
            .mlink => "Too many links.",
            .msgsize => "Message too large.",
            .multihop => "Reserved.",
            .nametoolong => "Filename too long.",
            .netdown => "Network is down.",
            .netreset => "Connection aborted by network.",
            .netunreach => "Network unreachable.",
            .nfile => "Too many files open in system.",
            .nobufs => "No buffer space available.",
            .nodev => "No such device.",
            .noent => "No such file or directory.",
            .noexec => "Executable file format error.",
            .nolck => "No locks available.",
            .nolink => "Reserved.",
            .nomem => "Not enough space.",
            .nomsg => "No message of the desired type.",
            .noprotoopt => "Protocol not available.",
            .nospc => "No space left on device.",
            .nosys => "Function not supported.",
            .notconn => "The socket is not connected.",
            .notdir => "Not a directory or a symbolic link to a directory.",
            .notempty => "Directory not empty.",
            .notrecoverable => "State not recoverable.",
            .notsock => "Not a socket.",
            .notsup => "Not supported, or operation not supported on socket.",
            .notty => "Inappropriate I/O control operation.",
            .nxio => "No such device or address.",
            .overflow => "Value too large to be stored in data type.",
            .ownerdead => "Previous owner died.",
            .perm => "Operation not permitted.",
            .pipe => "Broken pipe.",
            .proto => "Protocol error.",
            .protonosupport => "Protocol not supported.",
            .prototype => "Protocol wrong type for socket.",
            .range => "Result too large.",
            .rofs => "Read-only file system.",
            .spipe => "Invalid seek.",
            .srch => "No such process.",
            .stale => "Reserved.",
            .timedout => "Connection timed out.",
            .txtbsy => "Text file busy.",
            .xdev => "Cross-device link.",
            .notcapable => "Extension: Capabilities insufficient.",
            _ => null,
        };
    }

    pub fn format(num: Errno, writer: *std.Io.Writer) std.Io.Writer.Error!void {
        switch (num) {
            .success => try writer.writeAll(@tagName(num)),
            else => try writer.print("{t}: {s}", .{ num, num.message() orelse "unknown" }),
        }
    }

    const unexpected: Errno = @enumFromInt(404);

    pub fn mapError(err: Error) Errno {
        return switch (err) {
            error.MemoryAccessOutOfBounds => .fault,
            error.SystemResources,
            error.OutOfMemory,
            => .nomem,
            error.Unexpected => unexpected,

            error.AccessDenied => .notcapable, // .acces,
            error.NotCapable => .notcapable,
            error.BrokenPipe => .pipe,
            error.ConnectionResetByPeer => .connreset,
            error.ConnectionTimedOut => .timedout,
            error.DeadLock => .deadlk,
            error.DeviceBusy, error.FileBusy => .busy,
            error.DiskQuota => .dquot,
            error.FileNotFound, error.DirNotFound => .noent,
            error.DirNotEmpty => .notempty,
            error.FileTooBig => .fbig,
            error.InputOutput => .io,
            error.InvalidArgument => .inval,
            error.InvalidUtf8, error.BadPathName => .ilseq,
            error.IsDir => .isdir,
            error.LockedRegionLimitExceeded => .nolck,
            error.MessageTooBig => .msgsize,
            error.NameTooLong, error.PathTooLong => .nametoolong,
            error.NetworkNotFound => .netunreach,
            error.NoDevice => .nxio,
            error.NotDir => .notdir,
            error.NotSupported => .notsup,
            error.NoBufferSpace => .nobufs,
            error.NoSpaceLeft => .nospc,
            error.OperationAborted => .canceled,
            error.Overflow => .overflow,
            error.PathAlreadyExists => .exist,
            error.PermissionDenied => .perm,
            error.ProcessFdQuotaExceeded => .mfile,
            error.ProcessNotFound => .srch,
            error.SeekPipe => .pipe,
            error.SocketNotConnected => .notconn,
            error.SymLinkLoop => .loop,
            error.SystemFdQuotaExceeded => .nfile,
            error.WouldBlock,
            error.LockViolation,
            => .again,

            error.BadFd => .badf,
            error.Unimplemented => .nosys,
        };
    }
};

/// Includes a subset of the errors in `std.posix`.
pub const Error = std.mem.Allocator.Error ||
    wasmstint.runtime.MemInst.OobError ||
    std.posix.UnexpectedError ||
    error{
        AccessDenied,
        BrokenPipe,
        ConnectionResetByPeer,
        ConnectionTimedOut,
        DeadLock,
        DeviceBusy,
        DiskQuota,
        DirNotFound,
        DirNotEmpty,
        FileBusy,
        FileNotFound,
        FileTooBig,
        InputOutput,
        InvalidArgument,
        InvalidUtf8,
        IsDir,
        LockedRegionLimitExceeded,
        MessageTooBig,
        NameTooLong,
        NetworkNotFound,
        NoDevice,
        NotDir,
        NotSupported,
        NoBufferSpace,
        NoSpaceLeft,
        OperationAborted,
        Overflow,
        PathAlreadyExists,
        PermissionDenied,
        ProcessFdQuotaExceeded,
        ProcessNotFound,
        SeekPipe,
        SocketNotConnected,
        SymLinkLoop,
        SystemFdQuotaExceeded,
        WouldBlock,

        // Windows-specific
        BadPathName,
        SystemResources,
        LockViolation,

        BadFd,
        Unimplemented,
        PathTooLong,
        NotCapable,
    };

const std = @import("std");
const wasmstint = @import("wasmstint");
